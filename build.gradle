// plugins
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

dependencies {

    // algs4.jar
    compile name: 'algs4'

    // junit
    testCompile 'junit:junit:4.12'
    
}

findbugs {
    includeFilter = file("$rootProject.projectDir/config/findbugs/findbugs.xml")
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

idea {

    project.ipr {
        withXml { provider ->
            // GIT
            provider.node.component
                    .find { it.@name == 'VcsDirectoryMappings' }
                    .mapping.@vcs = 'Git'

            // Encoding
            def encodingComponent = provider.node.component.find { it.@name == 'Encoding'}
            if (encodingComponent) {
                encodingComponent.@useUTFGuessing = true
                encodingComponent.@native2AsciiForPropertiesFiles = true
                encodingComponent.@defaultCharsetForPropertiesFiles = 'UTF-8'
                encodingComponent.appendNode 'file', [url: 'PROJECT', charset: 'UTF-8']
            } else {
                def encodingNode = project.appendNode 'Encoding', [useUTFGuessing: true, native2AsciiForPropertiesFile: true, defaultCharsetForPropertiesFiles: 'UTF-8']
                encodingNode.appendNode 'file', [url: 'PROJECT', charset: 'UTF-8']
            }
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    flatDir {
        dirs 'lib'
    }
}

// wrapper
task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}